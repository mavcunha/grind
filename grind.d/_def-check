# vim: ft=zsh
# vim: ft=sh
SUBCOMMAND_DESC="Library to check current installation versus definitions"
SUBCOMMAND_HELP=$(cat <<EOH

Usage:

This is an internal library used by 'update' command to list
possible missed definitions.

Functions:

_dc_default_apps               Determine the list of default applications
                               in this macOS installation.

_dc_apps_installed             Returns a list of all applications installed
                               on this machine.

missing_apps                   Returns this list of apps which are not present
                               in defintions nor are default applications of this
                               OS.

EOH
)
use "brew"
use "mas"

# these need to be excluded from checks
typeset -ga _MAS_DEFAULT_APPS_MONTEREY=(
  'App Store' Automator Books Calculator Calendar Chess
  Contacts Dictionary FaceTime 'Find My' 'Font Book' 
  GarageBand Home iMovie Keynote Mail Maps Messages
  Music News Notes Numbers Pages 'Photo Booth' Photos
  Podcasts Preview Reminders Safari Shortcuts Stickies
  Stocks TextEdit TV VoiceMemos 'Time Machine' 'Image Capture'
  'Launchpad' 'Mission Control' 'QuickTime Player' Siri 
  'System Preferences' 'FindMy')


function _dc_default_apps() {
  # default apps
  local osname=$(macos_name)
  local default_apps="_MAS_DEFAULT_APPS_${(U)osname}"
  log "loading default apps for '${osname}': ${(j/,/P)default_apps[@]}"
  echo "${(P)default_apps}"
}

function _dc_apps_installed() {
  # list files in /Applications and /System/Applications, ignore ':' and empty lines
  local apps_installed="${(f)$(ls /Applications /System/Applications | grep -E '(.app|.localized)' | grep -vE '(:|^$)')}"
  log "found installed: ${apps_installed}"
  echo "${apps_installed}"
}

function missing_apps() {
  # gather applications requested by brew cask and mas
  local -a cask_apps=("${(@f)$(brew_cask_requested)}")
  local -a mas_apps=("${(@f)$(mas_requested)}")
  log "caks_apps: ${(j/,/)cask_apps}"
  log "mas_apps: ${(j/,/)mas_apps}"

  local -a all_requested=(${cask_apps}  ${mas_apps})

  # gather default apps for this macOS
  local -a default_apps=(${(@f)$(_dc_default_apps)})

  local extra_apps=""
  # loop on installed apps, check if is default, requested or extra
  for app in "${(@f)$(_dc_apps_installed)}"; do
    local app_name="${app:t:r}"
    if grep -q ${app_name} <<<"${(@f)default_apps}"; then
      log "[default]\t${app_name}"
    elif ! grep -q ${app_name} <<<"${(@f)all_requested}"; then
      log "[extra_app]\t${app_name}"
      extra_apps+="${app_name}\n"
    else
      log "[requested]\t${app_name}"
    fi
  done

  echo "${extra_apps}"
}
